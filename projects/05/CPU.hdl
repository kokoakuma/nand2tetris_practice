// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    And(a=instruction[15], b=instruction[4], out=loadD); // 命令が計算命令、かつ、dest保存先にDを含む場合
    DRegister(in=outALU, load=loadD, out=outD); // Dレジスタ

    Mux16(a=instruction, b=outALU, sel=instruction[15], out=outMUX0); // 命令が計算命令なら、命令を選択する

    Not(in=instruction[15], out=noti15);
    Or(a=noti15, b=instruction[5], out=loadA); // 命令がアドレス命令、または、dest保存先にAを含む場合
    ARegister(in=outMUX0, load=loadA, out=outA, out[0..14]=addressM);

    Mux16(a=outA, b=inM, sel=instruction[12], out=outMUX1); // aビットが１の時、Aレジスタの値を出力、aビットが０の時、メモリ[Aレジスタ]の値を出力

    ALU(x=outD, y=outMUX1, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=outALU, zr=zr, ng=ng); // ALUに値を入れる

    And(a=instruction[3], b=instruction[15], out=writeM); // 命令が計算命令、かつ、dest保存先にMを含む場合、メモリに書き込むフラグをtrueにする

    And(a=instruction[1], b=zr, out=outJump1); // ジャンプ命令あり、かつ、計算結果が０のフラグ
    And(a=instruction[2], b=ng, out=outJump2); // ジャンプ命令あり、かつ、計算結果が<0のフラグ

    Or(a=outJump1, b=outJump2, out=outJump1or2);

    Not(in=zr, out=notzr);
    Not(in=ng, out=notng);
    And(a=notzr, b=notng, out=positive); // 0でもなく、-でもない = 0より大きい
    And(a=instruction[0], b=positive, out=outJump0); // ジャンプ命令あり、かつ、計算結果が>0のフラグ

    Or(a=outJump0, b=outJump1or2, out=outJump); // ジャンプフラグ

    And(a=outJump, b=instruction[15], out=loadPC); // ジャンプフラグがtrue、かつ、命令が計算命令なら、アドレスをプログラムカウンタに書き込むフラグをtrueにする

    Not(in=outJump, out=notJump);
    Or(a=notJump, b=noti15, out=incPC); // ジャンプフラグなし、または、命令がアドレス命令なら、プログラムカウンタをインクリメントする

    PC(in=outA, load=loadPC, inc=incPC, reset=reset, out[0..14]=pc); // PGの処理
}